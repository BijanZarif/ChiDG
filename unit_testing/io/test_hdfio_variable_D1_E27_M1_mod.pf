module test_hdfio_variable_D1_E27_M1_mod
    use pfunit_mod
    use type_chidg,     only: chidg_t
    use mod_kinds,      only: rk,ik
    use mod_constants,  only: ONE, TWO, THREE, FOUR, FIVE, SIX, SEVEN, EIGHT
    use mod_hdfio,      only: read_grid_hdf, read_var_hdf, write_var_hdf
    use type_chidg,     only: chidg_t
    use type_meshdata,  only: meshdata_t
    implicit none


    public  :: test_hdfio_variable_D1_E27_M1

    @TestCase
    type, extends(TestCase) ::  test_hdfio_variable_D1_E27_M1

        type(chidg_t)               :: chidg
        character(12)               :: filename = 'D1_E27_M1.h5'
    
    contains
        procedure   ::  setUp
        procedure   ::  tearDown
    end type test_hdfio_variable_D1_E27_M1



contains

    !> NOTE: make sure to use 'this' instead of 'self' since these are over-writting a derived type 
    !! procedure. The argument names have to match
    subroutine setUp(this)
        class(test_hdfio_variable_D1_E27_M1),    intent(inout)   :: this

        integer(ik)                     :: nterms_s, nterms_c
        type(meshdata_t), allocatable   :: meshdata(:)

        !
        ! Initialize ChiDG environment
        !
        call this%chidg%init('env')


        !
        ! Read grid data from file 
        !
        call read_grid_hdf(this%filename, meshdata)
    

        !
        ! Initialize chidg domain
        !
        nterms_s = 8
        call this%chidg%data%add_domain(meshdata(1)%name,meshdata(1)%points,meshdata(1)%nterms_c,'scalar',nterms_s)
        call this%chidg%data%init_sdata()

    end subroutine

    
    subroutine tearDown(this)
        class(test_hdfio_variable_D1_E27_M1),    intent(inout)   :: this
        
    end subroutine




!-----------------------------------------------------------------------------
!   TESTS
!-----------------------------------------------------------------------------


@Test
    subroutine test_hdfio_variable_write_read_D1_E27_M1(this)
        class(test_hdfio_variable_D1_E27_M1),     intent(inout)   :: this 
        real(rk)                    :: tol, ref(8)
        integer(ik)                 :: ielem
        
        tol = 1.e-14_rk
        
        
        associate ( q => this%chidg%data%sdata%q%dom(1)%lvecs )
            
            ! Set reference solution values
            ref = [ONE, TWO, THREE, FOUR, FIVE, SIX, SEVEN, EIGHT]
            
            
            ! Assign reference solution
            do ielem = 1,this%chidg%data%mesh(1)%nelem
                q(ielem)%vec = ref
            end do
            
            !
            ! Write variable
            !
            call write_var_hdf(this%filename,'u',1,this%chidg%data)
            

            ! Reset variable
            do ielem = 1,this%chidg%data%mesh(1)%nelem
                q(ielem)%vec = -1._rk
            end do
            

            !
            ! Read variable
            !
            call read_var_hdf(this%filename,'u',1,this%chidg%data)
           


            ! Test that the read solution values equal the reference values
            ! Testing elements 1-10, out of 27
            @assertEqual(ref,q(1)%vec, tol)
            @assertEqual(ref,q(2)%vec, tol)
            @assertEqual(ref,q(3)%vec, tol)
            @assertEqual(ref,q(4)%vec, tol)
            @assertEqual(ref,q(5)%vec, tol)
            @assertEqual(ref,q(6)%vec, tol)
            @assertEqual(ref,q(7)%vec, tol)
            @assertEqual(ref,q(8)%vec, tol)
            @assertEqual(ref,q(9)%vec, tol)
            @assertEqual(ref,q(10)%vec, tol)        
       end associate
            
    end subroutine


end module test_hdfio_variable_D1_E27_M1_mod
