macro (add_test_target target_subdirectory)
	# set target name
	set(target_name pftest_${ARGV0})


	# Create folder for parsed test files in the build directory
	file(MAKE_DIRECTORY ${CMAKE_BINARY_DIR}/generated/${ARGV0})
	
	# Create input file for registering tests
	file(REMOVE ${CMAKE_BINARY_DIR}/generated/${ARGV0}/testSuites.inc)
	
	# Include directory created above so the .inc file is available
	include_directories(${CMAKE_BINARY_DIR}/generated/${ARGV0})
	
	# Include access to pfunit module
	include_directories(${PFUNIT}/mod)
	

	######################################################################
    # Loop through each test file, process with pFUnitParser and generate list of output files for compiler
    # DEPENDS option ensures that if the .pf file is changed, the parser will be called to reprocess the file
    ######################################################################
	set(_test_sources)
    foreach (_test ${ARGN})
    	# Get filename without extension (NAME_WE)
    	get_filename_component(filename ${_test} NAME_WE)
    	
    	# Test preprocessor command
    	add_custom_command(
        	OUTPUT ${CMAKE_BINARY_DIR}/generated/${ARGV0}/${filename}.F90
        	COMMAND ${PFUNIT}/bin/pFUnitParser.py ${CMAKE_SOURCE_DIR}/unittests/${ARGV0}/${filename}.pf ${CMAKE_BINARY_DIR}/generated/${ARGV0}/${filename}.F90
        	DEPENDS ${CMAKE_SOURCE_DIR}/unittests/${ARGV0}/${filename}.pf
        	)
        	
        # Accumulate list of test sources to define target
     	set(_test_sources ${_test_sources} ${CMAKE_BINARY_DIR}/generated/${ARGV0}/${filename}.F90)
     	
     	# Register test in input .inc file for pftest
     	file(APPEND ${CMAKE_BINARY_DIR}/generated/${ARGV0}/testSuites.inc "ADD_TEST_SUITE(${filename}_suite)\n")
	endforeach()
	
	set_source_files_properties(${PFUNIT}/include/driver.F90 PROPERTIES GENERATED 1)
	
	######################################################################
	## define test target and link with pfunit and coredg libraries
	######################################################################
	add_executable(${target_name} ${PFUNIT}/include/driver.F90 ${_test_sources})
	target_link_libraries(${target_name} ${PFUNIT}/lib/libpfunit.a coredg)
	
	
	######################################################################
	##	Add test that can be run with 'make test' or 'ctest'
	######################################################################
	add_test(NAME ${target_name}
			 WORKING_DIRECTORY ${CMAKE_RUNTIME_OUTPUT_DIRECTORY}
			 COMMAND $<TARGET_FILE:${target_name}>)
	
endmacro()
